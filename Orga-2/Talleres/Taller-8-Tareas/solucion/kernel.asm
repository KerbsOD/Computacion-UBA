; ** por compatibilidad se omiten tildes **
; ==============================================================================
; TALLER System Programming - ORGANIZACION DE COMPUTADOR II - FCEN
; ==============================================================================

%include "print.mac"
global start

; COMPLETAR - Agreguen declaraciones extern según vayan necesitando
extern A20_enable
extern A20_check
extern GDT_DESC
extern IDT_DESC
extern screen_draw_layout
extern pic_reset
extern pic_enable
extern idt_init
extern mmu_init_kernel_dir
extern tss_init
extern tasks_screen_draw
extern sched_init
extern tasks_init


; COMPLETAR - Definan correctamente estas constantes cuando las necesiten
%define CS_RING_0_SEL (1<<3) 
%define DS_RING_0_SEL (3<<3)  
; Cada selector ocupa 8 bytes.
; Si yo quiero ir al descritor 1 tengo que ir a 1x8bytes.
; Si yo quiero ir al descriptor 3 tengo que ir a 3x8bytes

BITS 16
;; Saltear seccion de datos
jmp start

;;
;; Seccion de datos.
;; -------------------------------------------------------------------------- ;;
start_rm_msg db     'Iniciando kernel en Modo Real'
start_rm_len equ    $ - start_rm_msg

start_pm_msg db     'Iniciando kernel en Modo Protegido'
start_pm_len equ    $ - start_pm_msg

DIVISOR equ 0x600

;;
;; Seccion de código.
;; -------------------------------------------------------------------------- ;;

;; Punto de entrada del kernel.
BITS 16
start:
    ; COMPLETAR - Deshabilitar interrupciones
    cli

    ; Cambiar modo de video a 80 X 50
    mov ax, 0003h
    int 10h ; set mode 03h
    xor bx, bx
    mov ax, 1112h
    int 10h ; load 8x8 font

    ; COMPLETAR - Imprimir mensaje de bienvenida - MODO REAL
    ; (revisar las funciones definidas en print.mac y los mensajes se encuentran en la
    ; sección de datos)
    print_text_rm start_rm_msg, start_rm_len, 0x5, 0, 0

    ; COMPLETAR - Habilitar A20
    ; (revisar las funciones definidas en a20.asm)
    call A20_enable
    call A20_check

    ; COMPLETAR - Cargar la GDT
    lgdt [GDT_DESC]

    ; COMPLETAR - Setear el bit PE del registro CR0
    mov eax, cr0
    or  eax, 1
    mov cr0, eax

    ; COMPLETAR - Saltar a modo protegido (far jump)
    jmp CS_RING_0_SEL:modo_protegido
    ; (recuerden que un far jmp se especifica como jmp CS_selector:address)
    ; Pueden usar la constante CS_RING_0_SEL definida en este archivo

BITS 32
modo_protegido:
    ; COMPLETAR - A partir de aca, todo el codigo se va a ejectutar en modo protegido
    ; Establecer selectores de segmentos DS, ES, GS, FS y SS en el segmento de datos de nivel 0
    ; Pueden usar la constante DS_RING_0_SEL definida en este archivo
    mov eax, DS_RING_0_SEL
    mov ds, eax
    mov es, eax
    mov gs, eax
    mov fs, eax
    mov ss, eax 

    ; COMPLETAR - Establecer el tope y la base de la pila
    mov esp, 0x25000
    mov ebp, esp

    ; COMPLETAR - Imprimir mensaje de bienvenida - MODO PROTEGIDO
    print_text_pm start_pm_msg, start_pm_len, 0x5, 0, 0

    ; Inicializamos Interrupciones
    call idt_init
    lidt [IDT_DESC]

    call pic_reset
    call pic_enable

    ; Inicializar antes del cr0 xd
    call mmu_init_kernel_dir
    mov cr3, eax
    
    ; Activamos Paginacion
    mov eax, cr0
    or  eax, 0x80000000
    mov cr0, eax

    call tss_init
    call sched_init
    call tasks_screen_draw
    call tasks_init

    ; Modificamos velocidad del pic
    mov ax, DIVISOR
    out 0x40, al
    rol ax, 8
    out 0x40, al

    xor eax, eax
    mov ax, (11<<3)
    ltr ax

    sti

    jmp (12<<3):0

    ; COMPLETAR - Inicializar pantalla
    call screen_draw_layout
    ; Ciclar infinitamente 
    .inicioCiclo:
    mov eax, 0xFFFF
    mov ebx, 0xFFFF
    mov ecx, 0xFFFF
    mov edx, 0xFFFF
    jmp .inicioCiclo

;; -------------------------------------------------------------------------- ;;

%include "a20.asm"